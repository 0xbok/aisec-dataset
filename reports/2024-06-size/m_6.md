## [M] Neither `sellCreditMarket()` nor `compensate()` checks whether the credit position to be sold is allowed for sale

<relative_path_091409:src/libraries/actions/SellCreditMarket.sol#L51-L122><relative_path_091409:src/libraries/actions/Compensate.sol#L42-L101>

### Impact

A credit buyer could be forced to buy a `not for sale` credit position with no way to sell it passively, potentially suffering a loss due to missing an arbitrage opportunity.

### Proof of Concept

Any user is allowed to lend or borrow `szaUSDC` through `Size` protocol.

- A borrower can create a limit borrowing order by calling `sellCreditLimit()`, anyone can lend their `szaUSDC` to this borrower by calling `buyCreditMarket()`.
- A lender can create a limit lending order by calling `buyCreditLimit()`, anyone can borrow `szaUSDC` from this lender by calling `sellCreditMarket()`.
- A existing credit position can be sold by its owner by `sellCreditMarket()` by default.
- A existing credit position by default could be bought by another borrower as long as the owner of credit position has created a limit borrowing order.

A lender can set their `allCreditPositionsForSaleDisabled` to `true`, which prevents any of their credit positions from being sold. Alternatively, a specific credit position can be marked as `not for sale`, making it impossible to sell and for anyone to buy it.

While no one can buy a `not for sale` credit position, their owner can force to sell it or use it compensate their debt. Once sold, no one can buy it again due to its `not for sale` status, resulting the new owner potentially suffering a loss.

Copy below codes to [SellCreditMarketTest.t.sol](relative_path_091409:test/local/actions/SellCreditMarket.t.sol) and run `forge test --match-test test_SellCreditMarket_sellNotForSaleCreditPosition`:

```solidity
    function test_SellCreditMarket_sellNotForSaleCreditPosition() public {
        _deposit(bob, weth, 100e18);
        _deposit(alice, usdc, 100e6);
        _deposit(candy, usdc, 100e6);
        _deposit(james, usdc, 200e6);
        _buyCreditLimit(alice, block.timestamp + 365 days, YieldCurveHelper.pointCurve(365 days, 0.2e18));
        //@audit-info candy is willing to lend USDC with 20% APR
        _buyCreditLimit(candy, block.timestamp + 365 days, YieldCurveHelper.pointCurve(365 days, 0.2e18));
        //@audit-info candy is willing to borrow USDC with 10% APR
        _sellCreditLimit(candy, YieldCurveHelper.pointCurve(365 days, 0.1e18));
        uint256 tenor = 365 days;

        vm.startPrank(bob);
        uint256 apr = size.getLoanOfferAPR(alice, tenor);
        //@audit-info bob sell 120e6 credit to alice for szaUSDC
        size.sellCreditMarket(
            SellCreditMarketParams({
                lender: alice,
                creditPositionId: RESERVED_ID,
                amount: 120e6,
                tenor: tenor,
                deadline: block.timestamp,
                maxAPR: apr,
                exactAmountIn: true
            })
        );
        vm.stopPrank();

        uint256[] memory creditPositionIds = new uint256[](1);
        creditPositionIds[0] = CREDIT_POSITION_ID_START;
        _setUserConfiguration(alice, 1.7e18, true, false, creditPositionIds);
        //@audit-info the credit position is marked not for sale
        assertTrue(!size.getCreditPosition(CREDIT_POSITION_ID_START).forSale);

        //@audit-info however, alice can sell her `not for sale` credit position to candy
        vm.startPrank(alice);
        size.sellCreditMarket(
            SellCreditMarketParams({
                lender: candy,
                creditPositionId: CREDIT_POSITION_ID_START,
                amount: 120e6,
                tenor: tenor,
                deadline: block.timestamp,
                maxAPR: apr,
                exactAmountIn: true
            })
        );
        vm.stopPrank();
        //@audit-info candy james the lender of credit position
        assertEq(size.getCreditPosition(CREDIT_POSITION_ID_START).lender, candy);
        apr = size.getBorrowOfferAPR(candy, tenor);
        //@audit-info james can not buy the credit position from candy due to `not for sale`
        vm.startPrank(james);
        vm.expectRevert(abi.encodeWithSelector(Errors.CREDIT_NOT_FOR_SALE.selector, CREDIT_POSITION_ID_START));
        size.buyCreditMarket(
            BuyCreditMarketParams({
                borrower: candy,
                creditPositionId: CREDIT_POSITION_ID_START,
                amount: 120e6,
                tenor: 365 days,
                deadline: block.timestamp,
                minAPR: apr - 1,
                exactAmountIn: false
            })
        );
        vm.stopPrank();
        //@audit-info candy has to alter `forSale` status to `true` manually.
        _setUserConfiguration(candy, 1.7e18, false, true, creditPositionIds);
        //@audit-info the `forSale` status of the credit position is marked as `true`
       assertTrue(size.getCreditPosition(CREDIT_POSITION_ID_START).forSale);
       //@audit-info james bought the credit position successfully
        vm.startPrank(james);
        size.buyCreditMarket(
            BuyCreditMarketParams({
                borrower: candy,
                creditPositionId: CREDIT_POSITION_ID_START,
                amount: 120e6,
                tenor: 365 days,
                deadline: block.timestamp,
                minAPR: apr - 1,
                exactAmountIn: false
            })
        );
        vm.stopPrank();
        //@audit-info james became the lender of credit position
        assertEq(size.getCreditPosition(CREDIT_POSITION_ID_START).lender, james);
    }
```



As we can see, as a speculator, Candy might suffer a loss because the credit position she is forced to buy is not able to be bought from her by any other buyer.



