## [M] Because of the asset: `Share 1:1 Conversion`, if vault incurs a loss, the last user to withdraw will take the entire loss

Because of the 1:1 conversion rate in `PoolV3`, if the pool incur a loss due to liquidation from borrowed asset through `CDPVault`, last user to withdraw will take the full loss.

### Vulnerability details

Every user should be allowed to withdraw a fair share of what's is available in the vault. But as:

- The exchange rate is [always 1:1 for deposit and withdraw](relative_path_091409:src/PoolV3.sol#L429-L439)
- and losses can happen as the underlying can be borrowed,

There will be situations where the last user to withdraw will not be made whole.

Also, if the last user A is not whole, and another user B deposit to the vault, A can get its missing assets from B deposit and B will be at loss waiting for another deposit.

Scenario:

1. Alice and Bob deposit 10 ETH each to PoolV3, PoolV3 has 20 ETH.
2. Each user receive 10 shares, and there are 20 total shares.
3. Eve deposit collateral to CDPVault and borrow 5 ETH.
4. Eve [get liquidated](relative_path_091409:src/CDPVault.sol#L624), the [loss is 1 ETH ](relative_path_091409:src/PoolV3.sol#L550-L550).
5. [Treasury has 0 remaining assets](relative_path_091409:src/PoolV3.sol#L554) to cover the debt (works also if less than 1 ETH treasury).
6. PoolV3 has now 19 ETH, and Alice and Bob 10 shares each where the exchange rate is 1:1.
7. Alice withdraw 10 ETH with her 10 shares.
8. There's only 9 ETH left for Bob.

```solidity
File: src/PoolV3.sol
529:     function repayCreditAccount(
530:         uint256 repaidAmount,
531:         uint256 profit,
532:         uint256 loss
533:     )
...:
...: // ------- some code ------- //
...:
548:         if (profit > 0) {
549:             _mint(treasury, convertToShares(profit));
550:         } else if (loss > 0) {                         <@(1) //we're in this case when there's a loss
551:             address treasury_ = treasury;
552:             uint256 sharesInTreasury = balanceOf(treasury_);
553:             uint256 sharesToBurn = convertToShares(loss);
554:❌                if (sharesToBurn > sharesInTreasury) { <@(2) //sharesToBurn are capped to sharesInTreasury
555:                 unchecked {
556:                     emit IncurUncoveredLoss({
557:                         creditManager: msg.sender,
558:                         loss: convertToAssets(sharesToBurn - sharesInTreasury)
559:                     });
560:                 }
561:❌                     sharesToBurn = sharesInTreasury; <@(2)
562:             }
563:❌                _burn(treasury_, sharesToBurn);
564:         }
```

### Impact

Loss of funds for users. Unfair loss distribution among users, as the only last withdrawer will incur the entire loss of the vault

### Proof of Concept

Add this test to `src/test/unit/CDPVault.t.sol`:

```solidity
     function testAudit_PoolV3LastWithdrawerAtLossWhenBadDebt() public {
        CDPVault vault = createCDPVault(token, 150 ether, 0, 1.25 ether, 1 ether, 1 ether);
        createGaugeAndSetGauge(address(vault));
          liquidityPool.setLock(false);
          // removing the initial deposit made in setUp() so that PooLV3 is empt
          liquidityPool.withdraw(1_000_000 ether, address(this), address(this));

          address alice = makeAddr("alice");
          address bob = makeAddr("bob");
        address position = address(this);

          address poolUnderlying = liquidityPool.underlyingToken();

          // Alice deposits 50 ETH
          deal(poolUnderlying, alice, 50 ether);
          vm.startPrank(alice);
          IERC20(underlyingToken).approve(address(liquidityPool), 50 ether);
          liquidityPool.deposit(50 ether, alice);
          vm.stopPrank();

          // Bob deposits 50 ETH
          deal(poolUnderlying, bob, 50 ether);
          vm.startPrank(bob);
          IERC20(underlyingToken).approve(address(liquidityPool), 50 ether);
          liquidityPool.deposit(50 ether, bob);
          vm.stopPrank();

          // state after liquidation
          console.log("----------  initial state with borrowed assets ----------");
          console.log("PoolV3 shares balance: %e", liquidityPool.totalSupply());
          console.log("PoolV3 ETH balance: %e", IERC20(poolUnderlying).balanceOf(address(liquidityPool)));

          // Simulating a user depositing 100 collateral and borrowing 80 ETH through CDPVault
        _modifyCollateralAndDebt(vault, 100 ether, 80 ether);

        uint256 virtualDebtBefore = virtualDebt(vault, position);
          console.log("virtualDebt before: %e", virtualDebtBefore);

        vm.warp(block.timestamp + 365 days);

        uint256 virtualDebtAfter = virtualDebt(vault, position);
          console.log("virtualDebt after (accrued interest): %e", virtualDebtAfter);

        // Simulating liquidation of the position and bad debt
          uint256 repayAmount = 80 ether;
        _updateSpot(0.7 ether);
        mockWETH.approve(address(vault), repayAmount);
        vault.liquidatePositionBadDebt(position, repayAmount);

          // state after liquidation
          console.log("\r");
          console.log("----------  state after bad debt liquidation ----------");
          console.log("PoolV3 shares balance: %e", liquidityPool.totalSupply());
          console.log("PoolV3 ETH balance: %e", IERC20(poolUnderlying).balanceOf(address(liquidityPool)));
          console.log("... Still 100 shares in PoolV3, but only 90 ETH left for Alice and Bob ...");

          vm.prank(alice);
          liquidityPool.withdraw(50 ether, alice, alice);

          console.log("\r");
          console.log("----------  state after Alice withdraw ----------");
          console.log("PoolV3 shares balance: %e", liquidityPool.totalSupply());
          console.log("PoolV3 ETH balance: %e", IERC20(poolUnderlying).balanceOf(address(liquidityPool)));
          console.log("... Bob has 50 shares but can only withdraw 40 ETH ...");

          vm.prank(bob);
          vm.expectRevert("SafeCast: value must be positive");
          liquidityPool.withdraw(50 ether, bob, bob);
     }
```



Logs:

```solidity
Ran 1 test for src/test/unit/CDPVault.t.sol:CDPVaultTest
[PASS] testAudit_collateralDebt() (gas: 3980542)
Logs:
  ----------  initial state with borrowed assets ----------
  PoolV3 shares balance: 1e20
  PoolV3 ETH balance: 1e20
  virtualDebt before: 8e19
  virtualDebt after (accrued interest): 1.03138823529411764705e20

  ----------  state after bad debt liquidation ----------
  PoolV3 shares balance: 1e20
  PoolV3 ETH balance: 9e19
  ... Still 100 shares in PoolV3, but only 90 ETH left for Alice and Bob ...

  ----------  state after Alice withdraw ----------
  PoolV3 shares balance: 5e19
  PoolV3 ETH balance: 4e19
  ... Bob has 50 shares but can only withdraw 40 ETH ...

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.32ms (2.27ms CPU time)
```



