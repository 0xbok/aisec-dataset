## [M] Lack of slippage check while interacting with ERC4626 Vault in `PositionAction4626` could lead to users' fund loss

Users' funds loss from ERC4626 exchange rate change or manipulated.

### Proof of Concept

If users specify `collateralParams.targetToken` as ERC4626's underlying token, `PositionAction4626` will attempt to deposit the `targetToken` to ERC4626 vault in order to retrieve the collateral token corresponding to the CDP Vault.

The execution flow:

See [`PositionAction._deposit`](relative_path_091409:src/proxy/PositionAction.sol#L502-L527)

```
    PositionAction.deposit --> PositionAction._deposit --> PositionAction4626._onDeposit
```

Here is the implementation used to convert from ERC4626's underlying to ERC4626 token:

See [`PositionAction4626._onDeposit`](relative_path_091409:src/proxy/PositionAction4626.sol#L41-L53)

```
    function _onDeposit(address vault, address /*position*/, address src, uint256 amount) internal override returns (uint256) {
        address collateral = address(ICDPVault(vault).token());

        // if the src is not the collateralToken, we need to deposit the underlying into the ERC4626 vault
        if (src != collateral) {
            address underlying = IERC4626(collateral).asset();
            IERC20(underlying).forceApprove(collateral, amount);
            amount = IERC4626(collateral).deposit(amount, address(this)); <-- No slippage check
        }

        IERC20(collateral).forceApprove(vault, amount);
        return ICDPVault(vault).deposit(address(this), amount);
    }
```

In this implementation, there is no slippage check when performing a deposit on target ERC4626's vault. This could leads to users' fund loss from slippage as ERC4626's exchange rate can change or be manipulated while users' transaction is pending.

This also happens in `withdraw` execution flow:

```
    PositionAction.withdraw --> PositionAction._withdraw --> PositionAction4626._onWithdraw  
```

If users specify a different `dst` (destination) token, it attempts to redeem that token from ERC4626 vault.

```
    function _onWithdraw(address vault, address /*position*/, address dst, uint256 amount) internal override returns (uint256) {
        uint256 collateralWithdrawn = ICDPVault(vault).withdraw(address(this), amount);

        // if collateral is not the dst token, we need to withdraw the underlying from the ERC4626 vault
        address collateral = address(ICDPVault(vault).token());
        if (dst != collateral) {
            collateralWithdrawn = IERC4626(collateral).redeem(collateralWithdrawn, address(this), address(this)); <-- No slippage check
        }

        return collateralWithdrawn;
    }
```



