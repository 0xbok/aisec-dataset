## [M] There is no slippage check in the `nuke()` function

NFT owners may receive significantly less nuke funds than they expected.

### Proof of Concept

In the `nuke()` function, there is no minimum claim amount check. Consider the following scenario:

1. The `fund` value of the `NukeFund` contract is 10 ETH.
2. Alice is about to nuke an NFT with a nuke factor of 0.5, allowing her to receive 5 ETH.
3. Bob also has an NFT with a nuke factor of 0.5 and calls the `nuke()` function with his NFT, front-running Alice.
    - Bob receives 5 ETH (see `L166`).
    - The remaining fund is now `10 ETH - 5 ETH = 5 ETH` (see `L174`).
4. Next, Alice's transaction is processed, and she receives only 2.5 ETH (0.5 of the remaining fund of 5 ETH).

If Alice's transaction had been processed before Bob's, she would have received 5 ETH. However, due to being front-run by Bob, she only receives 2.5 ETH, which is significantly less than expected. This highlights the necessity of implementing a minimum claim amount check in the `nuke()` function. With such a check, it is possible to prevent Alice from receiving only 2.5 ETH which is far below her expectations.

```solidity
    function nuke(uint256 tokenId) public whenNotPaused nonReentrant {
      require(
        nftContract.isApprovedOrOwner(msg.sender, tokenId),
        'ERC721: caller is not token owner or approved'
      );
      require(
        nftContract.getApproved(tokenId) == address(this) ||
          nftContract.isApprovedForAll(msg.sender, address(this)),
        'Contract must be approved to transfer the NFT.'
      );
      require(canTokenBeNuked(tokenId), 'Token is not mature yet');

      uint256 finalNukeFactor = calculateNukeFactor(tokenId); // finalNukeFactor has 5 digits
166   uint256 potentialClaimAmount = (fund * finalNukeFactor) / MAX_DENOMINATOR; // Calculate the potential claim amount based on the finalNukeFactor
      uint256 maxAllowedClaimAmount = fund / maxAllowedClaimDivisor; // Define a maximum allowed claim amount as 50% of the current fund size

      // Directly assign the value to claimAmount based on the condition, removing the redeclaration
      uint256 claimAmount = finalNukeFactor > nukeFactorMaxParam
        ? maxAllowedClaimAmount
        : potentialClaimAmount;

174   fund -= claimAmount; // Deduct the claim amount from the fund

      nftContract.burn(tokenId); // Burn the token
      (bool success, ) = payable(msg.sender).call{ value: claimAmount }('');
      require(success, 'Failed to send Ether');

      emit Nuked(msg.sender, tokenId, claimAmount); // Emit the event with the actual claim amount
      emit FundBalanceUpdated(fund); // Update the fund balance
    }
```



